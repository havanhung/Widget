plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs'
}

android {
    compileSdk 31
    def versionProperties = file('versions.properties')
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionProperties))

    def appVersionCode = versionProps['appVersionCode'].toInteger()
    def appVersionName = versionProps['appVersionName'].toString()
    def appDatabaseVersion = versionProps['appDatabaseVersion'].toString()
    def accountDatabaseVersion = versionProps['accountDatabaseVersion'].toString()

    defaultConfig {
        applicationId "com.sotatek.widget"
        minSdk 23
        targetSdk 31
        versionCode appVersionCode
        versionName appVersionName
        flavorDimensions "default"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "int", "APP_DATABASE_VERSION", appDatabaseVersion
        buildConfigField "int", "ACCOUNT_DATABASE_VERSION", accountDatabaseVersion
    }
    signingConfigs {
        dev {
            keyAlias 'debug'
            keyPassword '12345678'
            storeFile file(rootDir.getAbsolutePath() + '/keystores/keystore-debug.jks')
            storePassword '12345678'
        }
        pro {
            keyAlias 'pro'
            keyPassword '12345678'
            storeFile file(rootDir.getAbsolutePath() + '/keystores/keystore-pro.jks')
            storePassword '12345678'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.dev
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.pro
        }
    }
    productFlavors {
        pro {
            applicationId 'com.sotatek.widget.pro'
            resValue 'string', 'app_name', 'widget'
            signingConfig signingConfigs.pro
        }
        dev {
            applicationId 'com.sotatek.widget.dev'
            resValue 'string', 'app_name', 'widget - DEV'
            signingConfig signingConfigs.dev
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
    }

}
ext {
    life_cycle_version = '2.2.0'
    nav_version = '2.3.5'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar'])

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.fragment:fragment-ktx:1.4.0"

    // Jetpack
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'


    implementation("androidx.navigation:navigation-runtime-ktx:$nav_version")
    implementation("androidx.navigation:navigation-fragment-ktx:$nav_version")
    implementation("androidx.navigation:navigation-ui-ktx:$nav_version")

    implementation "androidx.lifecycle:lifecycle-extensions:$life_cycle_version"
    kapt "androidx.lifecycle:lifecycle-common-java8:$life_cycle_version"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"

    // Retrofit + GSON
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Utils
    implementation 'com.github.GrenderG:Toasty:1.5.0'
}